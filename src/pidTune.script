///////////////////////////////////////////////////////////////////////////////
/// @fn luxController 1
/// @brief Program loop
///////////////////////////////////////////////////////////////////////////////
if(state.txt=="null")
{
  thsp=0 //Disable sleep
  state.txt="init"
}
if(state.txt=="init")
{
  click checkHeaters,1
}
if(state.txt=="cooling")
{
  if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
  {
    state.txt="ready"
  }
}
if(state.txt=="ready")
{
  click tune,1
  state.txt="tuning"
}
if(state.txt=="tuning")
{
  oProgress.val=cycle.val*100/totalCycles.val
  if(oProgress.val==100)
  {
    state.txt="complete"
    click luxView,1
    click finish,1
  }
}
click luxView,1

///////////////////////////////////////////////////////////////////////////////
/// @fn finish
/// @brief Save the results and direct to confirmation
///////////////////////////////////////////////////////////////////////////////
diagConfirm.tMessage.txt="PID Tuning Complete"
DiagReturnPage=MAIN
page diagConfirm

///////////////////////////////////////////////////////////////////////////////
/// @fn tune
/// @brief Perfrom PID Autotune on hotends and bed
///////////////////////////////////////////////////////////////////////////////
commands.queue.txt+="M106 S255\rM155 S1\r" //Turn on fan.
click luxSend,0
delay=500
commands.queue.txt+="M303 E0 S220 U\r" //T0 PID Tune
commands.queue.txt+="M303 E1 S220 U\r" //T1 PID Tune
commands.queue.txt+="M303 E-1 S70 U\r" //Bed PID Tune
commands.queue.txt+="M500\r" //Save
click luxSend,0

///////////////////////////////////////////////////////////////////////////////
/// @fn checkHeaters
/// @brief Hotbed must be cool
///////////////////////////////////////////////////////////////////////////////
if(printer.bedTemp.val>40) //Stop if heatbed is too hot
{
  diagConfirm.tMessage.txt="Hot bed must be below\r40C"
  DiagReturnPage=MAIN
  page diagConfirm
}
if(printer.T0Temp.val<40&&printer.T1Temp.val<40)
{
  state.txt="ready"
}else
{
  if(state.txt!="cooling")
  {
    commands.queue.txt="M106 S255\rM104 T0 S0\rM104 T1 S0\rM140 S0\r" //Turn off heaters
    click luxSend,0
    state.txt="cooling"
  }
}

///////////////////////////////////////////////////////////////////////////////
/// @fn checkHotbed
/// @brief Hotbed must be cool
///////////////////////////////////////////////////////////////////////////////
if(printer.bedTemp.val>40) //Stop if heatbed is too hot
{
  click luxInit,0 //Cleanup
  diagConfirm.tMessage.txt="Hot bed must be below\r40C"
  DiagReturnPage=MAIN
  page diagConfirm
}

///////////////////////////////////////////////////////////////////////////////
/// @fn luxInit 0
/// @brief Call when perminently exiting the page
///////////////////////////////////////////////////////////////////////////////
thsp=SLEEP_TIME
if(state.txt=="tuning")
{
  state.txt="cancelling"
  click luxView,1
  click luxStopPrint,0
}
commands.command.txt="M155 S5" //Update temp slower
click luxSend,1
commands.command.txt="M107" //Turn off fan.
click luxSend,1
