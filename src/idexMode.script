///////////////////////////////////////////////////////////////////////////////
/// @fn luxController,0
/// @brief Custom on page callback handling
///////////////////////////////////////////////////////////////////////////////
click setIdexMode,1

///////////////////////////////////////////////////////////////////////////////
/// @fn luxView,1
/// @brief Updates states for controls
///////////////////////////////////////////////////////////////////////////////
bManual.picc=bManual.picc2+1
bAuto.picc=bAuto.picc2+1
bClone.picc=bClone.picc2+1
bMirror.picc=bMirror.picc2+1
if(IdexMode==0)
{
  bManual.picc=bManual.picc2
}
if(IdexMode==1)
{
  bAuto.picc=bAuto.picc2
}
if(IdexMode==2)
{
  bClone.picc=bClone.picc2
}
if(IdexMode==3)
{
  bMirror.picc=bMirror.picc2
}

///////////////////////////////////////////////////////////////////////////////
/// @fn setIdexMode
/// @breif Set idex mode from a return value
///////////////////////////////////////////////////////////////////////////////
if(ReturnVal>-1)
{
  if(ReturnVal==3)
  {
    commands.queue.txt="G28 X\r"
    commands.queue.txt+="M605 S2\r" //To use Mirror Mode, Clone Mode must first be set
    commands.queue.txt+="M605 S3\r"
  }else
  {
    commands.queue.txt="G28 X\r"
    commands.queue.txt+="M605 S"
    covx ReturnVal,vars.s.txt,0,0
    commands.queue.txt+=vars.s.txt
    commands.queue.txt+="\r"
  }
  click luxSend,0
  IdexMode=ReturnVal
  page MAIN
}

///////////////////////////////////////////////////////////////////////////////
//bManual
if(IdexMode!=0)
{
  diagPrompt.tMessage.txt="Switch to Manual Mode?"
  DiagReturnPage=dp
  ReturnObj=0
  ReturnVal=0
  page diagPrompt
}

//bAuto
if(IdexMode!=1)
{
  diagPrompt.tMessage.txt="Switch to Auto Mode?"
  DiagReturnPage=dp
  ReturnObj=0
  ReturnVal=1
  page diagPrompt
}

//bClone
if(IdexMode!=2)
{
  diagPrompt.tMessage.txt="Switch to Clone Mode?"
  DiagReturnPage=dp
  ReturnObj=0
  ReturnVal=2
  page diagPrompt
}

//bMirror
if(IdexMode!=3)
{
  diagPrompt.tMessage.txt="Switch to Mirror Mode?"
  DiagReturnPage=dp
  ReturnObj=0
  ReturnVal=3
  page diagPrompt
}